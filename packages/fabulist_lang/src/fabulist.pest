// SILENT RULES
WHITESPACE = _{ " " | "\n" }

// KEYWORDS
true_keyword  = _{ "true" }
false_keyword = _{ "false" }
none_keyword  = _{ "none" }
keyword       = _{ true_keyword | false_keyword | none_keyword }

// PRIMITIVE VALUE
string_interior = ${ (!("\"") ~ ANY)* }
string          = ${ "\"" ~ string_interior ~ "\"" }
number          = ${ ASCII_DIGIT+ }
boolean         = ${ true_keyword | false_keyword }
none            = ${ none_keyword }
identifier      = ${ !keyword ~ "_"? ~ ASCII_ALPHA+ ~ (ASCII_ALPHANUMERIC | "_")* }

// OPERATORS
unary_operator      = { "!" | "-" }
factor_operator     = { "/" | "*" }
term_operator       = { "-" | "+" }
comparison_operator = { ">" | ">=" | "<" | "<=" }
equality_operator   = { "!=" | "==" }
logical_operator    = { "&&" | "||" }

// DEFINITIONS
arguments       = { identifier ~ ("," ~ identifier)* }
object_interior = { string ~ ":" ~ expression ~ ("," ~ string ~ ":" ~ expression)* }
object          = { "{" ~ object_interior? ~ "}" }
grouping        = { "(" ~ expression ~ ")" }
id              = { "#" ~ string ~ ("=>" ~ object)? }
character       = { "[" ~ identifier ~ "]" }
quote           = { "-" ~ string ~ ("=>" ~ object)? }
dialogue        = { ">" ~ string ~ quote* }
monologue       = { character ~ dialogue+ }
function_body   = { "{" ~ action_statement* ~ "}" }
lambda_function = { "(" ~ arguments* ~ ")" ~ "=>" ~ function_body }

// EXPRESSIONS
primary    = _{ string | number | boolean | grouping | none | identifier | object | lambda_function }
call       =  { #callee = primary ~ ("(" ~ arguments* ~ ")")* }
unary      =  { call | (#operator = unary_operator ~ #right = (unary)+) }
factor     =  { #left = unary ~ (#operator = factor_operator ~ #right = unary)* }
term       =  { #left = factor ~ (#operator = term_operator ~ #right = factor)* }
comparison =  { #left = term ~ (#operator = comparison_operator ~ #right = term)* }
equality   =  { #left = comparison ~ (#operator = equality_operator ~ #right = comparison)* }
logical    =  { #left = equality ~ (#operator = logical_operator ~ #right = equality)* }
assignment =  { (#name = identifier ~ "=" ~ #value = logical) | logical }
expression =  { assignment }

// STATEMENTS
expression_statement = { expression ~ ";" }
action_statement     = { expression_statement }
node_statement       = { id ~ monologue* }
statement            = { node_statement }

fabulist = { SOI ~ statement ~ EOI }
